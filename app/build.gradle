plugins {
    id 'com.android.application'
}

def appName = 'Dr.Movil'
def appVersion = [
        name: { major, minor, patch, build, suffix = null ->
            def name = "$major.$minor.$patch"
            if (suffix) {
                name <<= "-$suffix"
            }
            return name.toString()
        },
        code: { major, minor, patch, build ->
            return (major * 10_000_000) + (minor * 100_000) + (patch * 1_000) + build
        }
]

android {
    compileSdk 31

    defaultConfig {
        applicationId "net.drmovil.app"
        minSdk 23
        targetSdk 31

        def (major, minor, patch, build) = [0, 1, 0, 0]

        versionName appVersion.name(major, minor, patch, build)
        versionCode appVersion.code(major, minor, patch, build)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }
        applicationVariants.all {
            variant ->
                variant.outputs.each {
                    output ->
                        output.outputFileName = "$appName-v${variant.versionName}-${variant.name}.apk"
                }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions 'default'
    productFlavors {
        staging {
            dimension 'default'
            applicationIdSuffix ".staging"

            signingConfigs {
                debug {
                    storeFile rootProject.file("keystore/staging.keystore")
                    storePassword 'android'
                    keyAlias 'staging'
                    keyPassword 'android'
                }

                release {
                    storeFile rootProject.file("keystore/staging.keystore")
                    storePassword 'android'
                    keyAlias 'staging'
                    keyPassword 'android'
                }
            }
        }
        playstore {
            dimension 'default'
            applicationIdSuffix ".playstore"
        }
        appgallery {
            dimension 'default'
            applicationIdSuffix ".appgallery"
        }
    }


}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}